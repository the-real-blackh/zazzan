#pragma version 6
q_safe:
    txn Fee
    int 10000
    <=
    txn CloseRemainderTo
    global ZeroAddress
    == 
    txn CloseRemainderTo
    global CreatorAddress
    ==
    ||
    &&
    txn RekeyTo
    global ZeroAddress
    ==
    &&
    txn OnCompletion  // UpdateApplication (4) or DeleteApplication (5)
    int 4
    >=
    txn Sender      // then sender must be creator
    global CreatorAddress
    !=
    &&
    !
    &&
    assert

app_calls:
    txn TypeEnum
    int 6
    ==
    txn OnCompletion   // No-Op or Opt-In, meaning it's either an app creation or an app call
    int 2
    <
    &&
    bz app_calls_end

q_on_creation:
    txn ApplicationID
    int 0
    ==
    bz q_on_creation_end

    pushbytes "init"
    int 1
    app_global_put

    int 1
    return

q_on_creation_end:

    txn ApplicationArgs 0
    pushbytes "setup"
    ==
    bz setup_end

    pushbytes "init"
    app_global_get
    txn NumAssets
    int 2
    ==
    &&
    assert

    pushbytes "init"
    app_global_del

    pushbytes "zan"
    txn Assets 0
    app_global_put

    pushbytes "fiat"
    txn Assets 1
    app_global_put

    itxn_begin
    int 4
    itxn_field TypeEnum
    txn Assets 0
    itxn_field XferAsset
    global CurrentApplicationAddress
    itxn_field AssetReceiver
    itxn_next

    int 4
    itxn_field TypeEnum
    txn Assets 1
    itxn_field XferAsset
    global CurrentApplicationAddress
    itxn_field AssetReceiver
    itxn_submit

    int 1
    return

setup_end:
    txn ApplicationArgs 0
    pushbytes "toZAN"
    ==
    bz toZAN_end

    global GroupSize
    int 2
    ==
    // Ensure we are a member of a transaction block where the first transaction is a
    // transfer of fiat to us.
    gtxn 0 TypeEnum
    int 4
    ==
    &&
    gtxn 0 XferAsset
    pushbytes "fiat"
    app_global_get
    ==
    &&
    gtxn 0 AssetReceiver
    global CurrentApplicationAddress
    ==
    &&
    assert

    // Transfer an equivalent amount of ZAN back to the sender.
    itxn_begin
    int 4
    itxn_field TypeEnum
    pushbytes "zan"
    app_global_get
    itxn_field XferAsset
    gtxn 0 AssetAmount
    itxn_field AssetAmount
    gtxn 0 Sender
    itxn_field AssetReceiver
    itxn_submit

    int 1
    return

toZAN_end:

    err

app_calls_end:
    int 1
