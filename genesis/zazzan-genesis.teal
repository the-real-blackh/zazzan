#pragma version 6
q_safe:
    txn Fee
    int 10000
    <=

    txn CloseRemainderTo
    global ZeroAddress
    == 
    txn CloseRemainderTo
    global CreatorAddress
    ==
    ||
    &&

    txn RekeyTo
    global ZeroAddress
    ==
    &&

    txn OnCompletion  // UpdateApplication (4) or DeleteApplication (5)
    int 4
    >=
    txn Sender        // then sender must be creator
    global CreatorAddress
    !=
    &&
    !
    &&

    assert

app_calls:
    txn TypeEnum
    int 6
    ==
    txn OnCompletion  // No-Op or Opt-In, meaning it's either an app creation or an app call
    int 2
    <
    &&
    bz app_calls_end

// Code to execute on creation of the application

q_on_creation:
    txn ApplicationID
    int 0
    ==
    bz q_on_creation_end

    int 1
    return

q_on_creation_end:

    txn ApplicationArgs 0
    pushbytes "setup"
    ==
    bnz app_call_setup

    err

app_calls_end:
    int 1
    return

// Initial setup of the application
app_call_setup:
    txn NumAppArgs
    int 2
    ==
    txn NumAssets
    int 2
    ==
    &&
    assert

    pushbytes "ZAN"
    txn Assets 0
    app_global_put

    pushbytes "USDC"
    txn Assets 1
    app_global_put

    itxn_begin
    int 4
    itxn_field TypeEnum
    txn Assets 0
    itxn_field XferAsset
    global CurrentApplicationAddress
    itxn_field AssetReceiver
    itxn_next

    int 4
    itxn_field TypeEnum
    txn Assets 1
    itxn_field XferAsset
    global CurrentApplicationAddress
    itxn_field AssetReceiver
    itxn_submit

    pushbytes "admFund"
    txn ApplicationArgs 1
    app_global_put

    // Things prefixed by c_ can be changed through a vote.

    // Exit tax for conversions of ZAN to fiat in basis points
    pushbytes "c_exitTax_bp"
    int 25
    app_global_put

    // Fraction of the total circulating supply of Zazzans to limit the admin fund to,
    // in basis points
    pushbytes "c_admFundFrac_bp"
    int 100
    app_global_put

    // The actual cap of the admin fund, calculated from the highest that
    // c_admFundFrac_bp * circulating supply ever reaches. If the admin fund ever
    // exceeds this value, then any ZANs added to it are burned.
    pushbytes "c_admFundCap_Z"
    int 0
    app_global_put

    // The cost of an election in Zazzans. At the conclusion of an election, the cost
    // is distributed to the voters.
    pushbytes "c_eleCost_Z"
    int 1000000         // 1 ZAN
    app_global_put

    // Your voting power is determined by your ZAN balance at the time you vote, but
    // the voting power is capped at a certain number of ZANs. The idea here is to
    // prevent individuals, organization or governments with deep pockets buying up
    // all the voting power.
    pushbytes "c_votePwrCap_Z"
    int 10000000000     // 10,000 ZANs
    app_global_put

    int 1
    return
